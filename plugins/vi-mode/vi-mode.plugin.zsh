function zle-line-init zle-keymap-select {
  zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

# binds the builtin edit-command-line function to 'v' when in command/normal
# mode
autoload -z edit-command-line
zle -N edit-command-line
bindkey -M vicmd v edit-command-line

bindkey -v

# if mode indicator wasn't setup by theme, define default
if [[ "$MODE_INDICATOR" == "" ]]; then
  MODE_INDICATOR="%{$fg_bold[red]%}<%{$fg[red]%}<<%{$reset_color%}"
fi

function vi_mode_prompt_info() {
  echo "${${KEYMAP/vicmd/$MODE_INDICATOR}/(main|viins)/}"
}

# define right prompt, if it wasn't defined by a theme
if [[ "$RPS1" == "" && "$RPROMPT" == "" ]]; then
  RPS1='$(vi_mode_prompt_info)'
fi

# set the pager to the vim less script, if it exists.
# this could easily be extended to look for it in other places.
if [[ -a "/usr/share/vim/vim73/macros/less.sh" ]]; then
    export PAGER=/usr/share/vim/vim73/macros/less.sh
fi

# set the editor to vim
export EDITOR=vim
